#!/bin/gnuplot

set terminal postscript eps size 10, 4 enhanced "Helvetica" 18
set output 'qd_output.eps'

set multiplot layout 2, 1
set tmargin 3

set title "<Without ClusterFetch>" offset -52, 0
set xlabel "Time (sec)"
set ylabel "Queue depth"

set obj 1 rect from 0.2, 0 to 7.4, 35 fc lt -1 fs solid 0.25 noborder
set obj 2 rect from 7.5, 0 to 15.8, 35 fc lt -1 fs solid 0.25 noborder
set obj 3 rect from 16.1, 0 to 26.8, 35 fc lt -1 fs solid 0.25 noborder
set obj 4 rect from 0, 0 to 0.1, 35 fc lt -1 fs pattern 2 noborder
set obj 5 rect from 27.5, 0 to 27.6, 35 fc lt -1 fs pattern 2 noborder
set style arrow 1 heads filled size 0.5, 45
set arrow 1 from 0.15, 7 to 27.35, 7 arrowstyle 1
set label "27.5s" at 13, 10 center

plot "wopf/queue_depth.txt" using ($1-384.4):2 lt 1 lc 1 with lines notitle
set xrange [0:30]

unset obj 1
unset obj 2
unset obj 3
unset obj 4
unset obj 5
unset arrow
unset label

set style line 1 lt 2 lc rgb "black" lw 1
set arrow 2 from first 0.1, 35 to first 0.1, screen 0.6 nohead ls 1
set arrow 3 from first 23.4, 35 to first 27.6, screen 0.6 nohead ls 1

set title "<With ClusterFetch>" offset -53, 0
set xlabel "Time (sec)"
set ylabel "Queue Depth"

set obj 1 rect from 0.2, 0 to 5.1, 35 fc lt -1 fs solid 0.25 noborder
set obj 2 rect from 6.8, 0 to 9.8, 35 fc lt -1 fs solid 0.25 noborder
set obj 3 rect from 15.4, 0 to 20, 35 fc lt -1 fs solid 0.25 noborder
set obj 4 rect from 0, 0 to 0.1, 35 fc lt -1 fs pattern 2 noborder
set obj 5 rect from 23.3, 0 to 23.4, 35 fc lt -1 fs pattern 2 noborder
set arrow 1 from 0.15, 7 to 23.15, 7 arrowstyle 1
set label "23.3s" at 13, 10 center

plot "wipf/queue_depth.txt" using ($1-349):2 lt 1 lc 1 with lines notitle
set xrange [0:30]

unset multiplot
